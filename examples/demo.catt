--
--  demo.catt - demonstation of catt typechecking
--

coh id (x : *) : * | x -> x ;

coh comp (x : *)
    	 (y : *) (f : * | x -> y)
	 (z : *) (g : * | y -> z)
	 : * | x -> z ;

coh unitl (x : *)
    	  (y : *) (f : * | x -> y)
	  : * | x -> y | comp x x (id x) y f -> f ;

coh unitr (x : *)
    	  (y : *) (f : * | x -> y)
	  : * | x -> y | comp x y f y (id y) -> f ;
	  
coh whiskerl (x : *)
    	     (y : *) (f : * | x -> y)
	     (z : *) (g : * | y -> z)
	     (h : * | y -> z) (a : * | y -> z | g -> h)
	     : * | x -> z | #comp x y f z g ->
	       	     	    #comp x y f z h ;
	     
coh whiskerr (x : *)
    	     (y : *) (f : * | x -> y)
	     (g : * | x -> y) (a : * | x -> y | f -> g)
	     (z : *) (h : * | y -> z)
	     : * | x -> z | #comp x y f z h ->
	       	     	    #comp x y g z h ;
	     
coh assoc (x : *)
    	  (y : *) (f : * | x -> y)
          (z : *) (g : * | y -> z)
	  (w : *) (h : * | z -> w)
	  : * | x -> w | #comp x z (comp x y f z g) w h ->
	      	       	 #comp x y f w (comp y z g w h) ;

coh vcomp (x : *)
    	  (y : *) (f : * | x -> y)
	  (g : * | x -> y) (a : * | x -> y | f -> g)
	  (h : * | x -> y) (b : * | x -> y | g -> h)
	  : * | x -> y | f -> h ;

