--
--  demo.catt - demonstation of catt typechecking
--

coh id (x : *) : * | x -> x ;

--
-- The following coherence for invertibility
-- fails as expected.
-- 
-- coh inv (x : *)
--     	(y : *) (f : * | x -> y)
-- 	: * | y -> x ;
--

coh comp (x : *)
    	 (y : *) (f : * | x -> y)
	 (z : *) (g : * | y -> z)
	 : * | x -> z ;

-- def compl (x : *)
--     	  (y : *) (f : * | x -> y)
-- 	  (z : *) (g : * | y -> z)
-- 	  (w : *) (h : * | z -> w)
-- 	  : * | x -> w = comp x z (comp x y f z g) w h ;

--
-- Represented as:
-- \x. \y. \f. \z. \g. \w. \h. comp x z (comp x y f z g) w h ;
--

--
-- def compLeft (x : *)
--              (y : *) (f : * | x -> y)
-- 	        (z : *) (g : * | y -> z)
-- 	        (w : *) (h : * | z -> w)
-- 	        : Î£ (* | x -> z) (\p -> * | z -> w) = (comp x y f z g , h) ;

coh unitl (x : *)
    	  (y : *) (f : * | x -> y)
	  : * | x -> y | #comp x x (#id x) y f -> f ;

coh unitr (x : *)
    	  (y : *) (f : * | x -> y)
	  : * | x -> y | #comp x y f y (#id y) -> f ;
	  
coh whiskerl (x : *)
    	     (y : *) (f : * | x -> y)
	     (z : *) (g : * | y -> z)
	     (h : * | y -> z) (a : * | y -> z | g -> h)
	     : * | x -> z | #comp x y f z g ->
	       	     	    #comp x y f z h ;
	     
coh whiskerr (x : *)
    	     (y : *) (f : * | x -> y)
	     (g : * | x -> y) (a : * | x -> y | f -> g)
	     (z : *) (h : * | y -> z)
	     : * | x -> z | #comp x y f z h ->
	       	     	    #comp x y g z h ;
	     
coh assocr (x : *)
    	   (y : *) (f : * | x -> y)
           (z : *) (g : * | y -> z)
	   (w : *) (h : * | z -> w)
	   : * | x -> w | #comp x z (#comp x y f z g) w h ->
	      	       	  #comp x y f w (#comp y z g w h) ;

coh vcomp (x : *)
    	  (y : *) (f : * | x -> y)
	  (g : * | x -> y) (a : * | x -> y | f -> g)
	  (h : * | x -> y) (b : * | x -> y | g -> h)
	  : * | x -> y | f -> h ;

coh hcomp (x : *)
    	  (y : *) (f : * | x -> y)
	  (g : * | x -> y) (a : * | x -> y | f -> g)
	  (z : *) (h : * | y -> z)
	  (k : * | y -> z) (b : * | y -> z | h -> k)
	  : * | x -> z | #comp x y f z h -> #comp x y g z k ;
	  
coh icomp (x : *)
    	  (y : *) (f : * | x -> y)
	  (g : * | x -> y) (a : * | x -> y | f -> g)
	  (h : * | x -> y) (b : * | x -> y | g -> h)
	  (z : *) (l : * | y -> z)
	  (m : * | y -> z) (c : * | y -> z | l -> m)
	  (n : * | y -> z) (d : * | y -> z | m -> n)
	  : * | x -> z | #comp x y f z l -> #comp x y h z n ;

coh ichg (x : *)
    	 (y : *) (f : * | x -> y)
	 (g : * | x -> y) (a : * | x -> y | f -> g)
	 (h : * | x -> y) (b : * | x -> y | g -> h)
	 (z : *) (l : * | y -> z)
	 (m : * | y -> z) (c : * | y -> z | l -> m)
	 (n : * | y -> z) (d : * | y -> z | m -> n)
	 : * | x -> z | #comp x y f z l -> #comp x y h z n | 
	     #hcomp x y f h (#vcomp x y f g a h b) z l n (#vcomp y z l m c n d) ->
	     #vcomp x z (#comp x y f z l)
	     	    (#comp x y g z m) (#hcomp x y f g a z l m c)
		    (#comp x y h z n) (#hcomp x y g h b z m n d) ;
